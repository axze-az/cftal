#! /usr/bin/sollya
execute("common.sollya");

/* implementpoly(p, [-log(2)/512, log(2)/512], 1e-30, DD, "exp_p", "exp.c"); */
/* printexpansion(p-1.0); */
verbosity=1!;
prec=1024!;
write("\n");

/* exp(x) f32 using tang's method */
N = 32;
f = exp(x);
f1 = expm1(x);
g=log(2)/2;
i=[-g/N, g/N];
exp_table(N, single);
b = [|1, ..., 3|];
c = [|SG...|];
p = fpminimax(f1, b, c, i, relative, floating);
write_approx_prec(p, f1, i);
write_coeffs(p, "constexpr\n    const float exp_c", "exp", b, c);

/* N/ln(2) */
write_coeff(N/log(2), "", "constexpr\n    const float _32_ln2", single);
/* ln(2)/N Cody and Waite */
c1h=round(log(2)/N, 24-7-5, RN);
c1l=log(2)/N-c1h;
write_coeff(c1h, "", "constexpr\n    const float _ln2_32_cw_h", single);
write_coeff(c1l, "", "constexpr\n    const float _ln2_32_cw_l", single);
/* ln(2)/N */
c1h=single(log(2)/N);
c1l=log(2)/N-c1h;
write_coeff(c1h, "", "constexpr\n    const float _ln2_32_h", single);
write_coeff(c1l, "", "constexpr\n    const float _ln2_32_l", single);

/* log_10(x) = lg(10) */
/* N/lg(2) */
write_coeff(N/(log(2)/log(10)), "", "constexpr\n    const float _32_lg2", single);
/* lg(2)/N Cody and Waite */
lg_2_N=log(2)/(log(10))/N;
c1h=round(lg_2_N, 24-7-5, RN);
c1l=lg_2_N-c1h;
write_coeff(c1h, "", "constexpr\n    const float _lg2_32_cw_h", single);
write_coeff(c1l, "", "constexpr\n    const float _lg2_32_cw_l", single);
/* lg(2)/N */
c1h=single(lg_2_N);
c1l=lg_2_N-c1h;
write_coeff(c1h, "", "constexpr\n    const float _lg2_32_h", single);
write_coeff(c1l, "", "constexpr\n    const float _lg2_32_l", single);

quit;

/* exp(x) f32 */
f= x*(exp(x)+1)/(exp(x)-1);
i = [0, log(2)/2];
b = [|0, 2, 4|];
c = [|SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p-2, "constexpr\n    const float exp_c", "exp", b, c);
// print_f_poly_coeffs(p-2, "constexpr\n    const float exp_c", "exp");
plot((p(x)-f(x))/f(x), [0, log(2)/2],
                       postscriptfile, "exp-f32");

f = (2*x/(exp(x)-1) - 2 +x)/(x^2);
i = [0x1p-512, log(2)/2];
d = guessdegree(f, i, 0x1p-28);
d;
b = [|0, 2, 4|];
c = [|SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_coeffs(p, "constexpr\n    const float expn_c", "expn", b, c);
plot((p(x)-f(x))/f(x), i,
                       postscriptfile, "exp-n-f32");

/* a simple polynomial for exp f32 */
f = exp(x);
i = [-log(2)/2, log(2)/2];
d = guessdegree(f, i, 0x1p-28);
d;
b = [|0, ..., 7|];
c = [|SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float exp_c", "exp", b, c);
plot((p(x)-f(x))/f(x), i, postscriptfile, "exp-f32");

/* a simple polynomial for higher precision exp f32 */
f = exp(x);
i = [-log(2)/2, log(2)/2];
d = guessdegree(f, i, 0x1p-28);
d;
b = [|0, ..., 8|];
c = [|SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float pow_exp_c", "pow_exp", b, c);
plot((p(x)-f(x))/f(x), i, postscriptfile, "pow-exp-f32");

/* expm1(x) f32 */
f= 6*((x*(exp(x)+1)/(exp(x)-1))-2)/(x^2);
p = fpminimax(f, [|2, 4 |], [|SG...|], [0x1p-512, log(2)/2], 1.0,
                 relative, floating);
print_f_poly_coeffs_pow_i(p-1, "constexpr\n    const float expm1_c", "expm1");
plot((p(x)-f(x))/f(x), p(x)-f(x), [0x1p-58, log(2)/2],
     postscriptfile, "expm1-f32");

/* exp2(x) f32 */
i = [-0.5, 0.5];
f = 2^x;
d = guessdegree(f, i, 0x1p-28);
d;
b = [|0, ... , 6|];
c = [|SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float exp2_c", "exp2", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x), i,
     postscriptfile, "exp2-f32");

/* exp10(x) f32 */
i = [-log(2)/log(10)/2, log(2)/log(10)/2];
f = 10^x;
d = guessdegree(f, i, 0x1p-28);
d;
b = [|0, ... , 6|];
c = [|SG...|];
p = fpminimax(f, b, c, i,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float exp10_c", "exp10", b, c);
write_coeff(log(10), "", "constexpr\n    const float log10", single);
write_coeff(log(10)*log(10), "", "constexpr\n    const float log10sqr", single);
plot((p(x)-f(x))/f(x), p(x)-f(x), i,
     postscriptfile, "exp10-f32");

/* log1p for log_hp implementation */
f= log1p(x);
ib=single(sqrt(2)/2);
printsingle(ib);
i= [ib-1, (ib*2)-1];
d = guessdegree(f, i, 0x1p-28);
d;
b = [|1, ..., 13|];
c = [|SG, SG, 49, SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float log_c", "log", b, c);

/* log1p for log implementation */
f= log1p(x);
ib=single(sqrt(2)/2);
printsingle(ib);
i= [ib-1, (ib*2)-1];
d = guessdegree(f, i, 0x1p-28);
d;
b = [|1, ..., 11|];
c = [|SG, SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float log_c", "log", b, c);

/* log(x) f32 */
f= log((1+x)/(1-x))/x-2.0;
i= [0x1p-58, (sqrt(2)-1)/(1+sqrt(2))];
d = guessdegree(f, i, 0x1p-28);
d;
b = [|2, 4, 6, 8|];
c = [|SG...|];
p = fpminimax(f, b, c, i,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float log_c", "log", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x), i,
     postscriptfile, "log-f32");

/* y= (x-1)/(x+1) */
/* log(x) f32 */
f= log((1+x)/(1-x));
i= [0x1p-58, (sqrt(2)-1)/(1+sqrt(2))];
d = guessdegree(f, i, 0x1p-40);
d;
b = [|1, 3, 5, 7, 9, 11|];
c = [|SG, SG...|];
p = fpminimax(f, b, c, i,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float pow_log_c", "pow_log", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x),
                       i,
                       postscriptfile, "pow-log-f32");
// implementpoly(p, i, 0x1p-38, D, "log", "log.c");


/* pow log with high precision */
/* y= (x-1)/(x+1) */
/* log(x) f64 */
f= log((1+x)/(1-x));
i= [0, (sqrt(2)-1)/(1+sqrt(2))];
d = guessdegree(f, i, 0x1p-40);
d;
b = [|1, 3, 5, 7, 9, 11|];
c = [|SG, 49, SG...|];
p = fpminimax(f, b, c, i,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float pow_log_hp_c", "pow_log_hp", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x),
                       i,
                       postscriptfile, "pow-log-hp-f32");


/* sin(x) f32 */
i = [0x1p-58, pi/4];
f = sin(x)/x;
b = [|2,4,6,8|];
c = [|SG...|];
p = fpminimax(f, b, c, i, 1.0,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float sin_c", "sin", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x), i,
                       postscriptfile, "sin-f32");

/* cos(x) f32 */
i = [0x1p-58, pi/4];
f = cos(x)-1+(x^2)/2;
b = [|4,6,8,10|];
c = [|SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float cos_c", "cos", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x), i,
                       postscriptfile, "cos-f32");

/* tan as polynomial in [0, pi/4] */
f = tan(x);
i = [0, pi/4];
d = guessdegree(f, i, 0x1p-28);
d;
b = [|1, 3, 5, 7, 9, 11, 13, 15|];
c = [|SG, SG...|];
p = fpminimax(f, b, c, i,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float tan_c", "tan", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x), i,
                       postscriptfile, "tan-f32");

/* tan(x) f32 */
f = tan(x)/x;
i = [0x1p-58, 6/7*pi/4];
b = [|2,4,6,8,10,12,14,16,18|];
c = [|SG...|];
p = fpminimax(f, b, c, i, 1.0,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float tan_sun_c", "tan", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x), i,
                       postscriptfile, "tan-sun-f32");

/* atan(x) f32, piecewise polynomial approximations */
f = atan(x);
i = [0, 0.375];
d = guessdegree(f, i, 0x1p-28);
d;
b = [|1, 3, 5, 7, 9|];
c = [|SG...|];
p = fpminimax(f, b, c, i,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float atan_i0_c", "atan_0", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x), i,
                       postscriptfile, "atan_i0-f32");

il= 0.375;
ir= 1.0;
im= dirtyfindzeros(atan(x)-(0.5*(atan(il)+atan(ir))), [il, ir])[0];
im= round(im, 10, RN);
i = [il-im, ir-im];
i;
im;
x0= im;
f = atan(x+x0);
d = guessdegree(f, i, 0x1p-28);
d;
b = [|0, ..., 10|];
c = [|49, SG...|];
p = fpminimax(f, b, c, i, relative, floating);
p;
write_approx_prec_shift(p, f, i, im);
write_coeffs(p, "constexpr\n    const float atan_i1_c", "atan_i1", b, c);
write_coeff(il, "", "constexpr\n    const float atan_i1_left", single);
write_coeff(ir, "", "constexpr\n    const float atan_i1_right", single);
write_coeff(x0, "", "constexpr\n    const float atan_i1_x0", single);
plot((p(x)-f(x))/f(x), i, postscriptfile, "atan_i1-f32");

/* asin(x) f32 */
i = [0x1p-58, 0.5];
f = (asin(x)-x)/(x^3);
b = [|0, 2, 4, 6, 8, 10, 12|];
c = [|SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float asin_c", "asin", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x), i,
                       postscriptfile, "asin-f32");

/* cbrt(x) f32 */
i = [2^-3, 1];
f = (x)^(1/3);
b= [|0,...,6|];
c= [|SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float cbrt_c", "cbrt", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x), i,
                       postscriptfile, "cbrt-f32");

/* cbrt(x) f32, original code
i = [2^-3, 1];
f = (x)^(1/3);
b= [|0,...,1|];
c= [|SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float cbrt_c", "cbrt", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x), i,
                       postscriptfile, "cbrt-f32");
*/

/* rcbrt(x) f32 */
i = [2^-3, 1];
f = (x)^-(1/3);
b= [|0,...,7|];
c= [|SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float rcbrt_c", "rcbrt", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x), i,
                       postscriptfile, "rcbrt-f32");

/* polynomial for sinh between 0 and 1.0 */
f = sinh(x);
i = [0, 1.0];
d = guessdegree(f, i, 0x1p-28);
d;
b = [|1, 3, 5, 7, 9|];
c = [|SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float sinh_c", "sinh", b, c);
write_coeff(sup(i), "", "constexpr\n    const float sinh_i0_right",
            single);

/* polynomial for sinh where (e^x-e^x)/2 fails */
f = sinh(x);
i = [0x1p-512, log(2)/4];
d = guessdegree(f, i, 0x1p-28);
d;
b = [|1, 3, 5|];
c = [|SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float sinh_c", "sinh", b, c);
write_coeff(sup(i), "", "constexpr\n    const float sinh_i0_right",
            single);

/* polynomial for cosh between 0 and 1.0 */
f = cosh(x);
i = [0, 1.0];
d = guessdegree(f, i, 0x1p-28);
d;
b = [|0, 2, 4, 6, 8, 10|];
c = [|SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float cosh_c", "cosh", b, c);
write_coeff(sup(i), "", "constexpr\n    const float cosh_i0_right",
            single);

/* polynomial for tanh where (e^2x-1)(e^2x+1) fails */
f = tanh(x);
i = [0x1p-512, log(2)/32];
d = guessdegree(f, i, 0x1p-28);
d;
b = [| 1, 3|];
c = [|SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float tanh_c", "tanh", b, c);
write_coeff(sup(i), "", "constexpr\n    const float tanh_i0_right",
            single);

/* rsqrt f32 */
f= 1.0/sqrt(x);
i= [2^(-2), 2^(-1)-2^(-5)];
d = guessdegree(f, i, 0x1p-12);
d;
b = [|0, ..., 2|];
c = [|SG...|];
p = fpminimax(f, b, c, i,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float rsqrt_i0_c", "rsqrt", b, c);
/* write_coeff(inf(i), "", "constexpr\n    const float rsqrt_i0_left", single); */
plot((p(x)-f(x))/f(x), p(x)-f(x),
                       i,
                       postscriptfile, "rsqrt-i0-f32");

f= 1.0/sqrt(x);
i= [2^(-1)-2^(-5), 2^(-0)];
d = guessdegree(f, i, 0x1p-12);
d;
b = [|0, ..., 2|];
c = [|SG...|];
p = fpminimax(f, b, c, i,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float rsqrt_i1_c", "rsqrt", b, c);
write_coeff(inf(i), "", "constexpr\n    const float rsqrt_i1_left", single);
plot((p(x)-f(x))/f(x), p(x)-f(x),
                       i,
                       postscriptfile, "rsqrt-i1-f32");

/* root12 f32 */
f= x^(1/12);
i= [2^(-12), 2^(-9)];
d = guessdegree(f, i, 0x1p-8);
d;
b = [|0, ..., 2|];
c = [|SG...|];
p = fpminimax(f, b, c, i,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float root12_i0_c", "root12_i0", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x),
                       i,
                       postscriptfile, "root12-i0-f32");

/* root12 f32 */
f= x^(1/12);
i= [2^(-9), 2^(-6)];
d = guessdegree(f, i, 0x1p-8);
d;
b = [|0, ..., 2|];
c = [|SG...|];
p = fpminimax(f, b, c, i,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float root12_i1_c", "root12_i1", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x),
                       i,
                       postscriptfile, "root12-i1-f32");

/* root12 f32 */
f= x^(1/12);
i= [2^(-6), 2^(-3)];
d = guessdegree(f, i, 0x1p-8);
d;
b = [|0, ..., 2|];
c = [|SG...|];
p = fpminimax(f, b, c, i,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float root12_i2_c", "root12_i2", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x),
                       i,
                       postscriptfile, "root12-i2-f32");

/* root12 f32 */
f= x^(1/12);
i= [2^(-3), 2^(-0)];
d = guessdegree(f, i, 0x1p-8);
d;
b = [|0, ..., 2|];
c = [|SG...|];
p = fpminimax(f, b, c, i,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float root12_i3_c", "root12_i3", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x),
                       i,
                       postscriptfile, "root12-i3-f32");


/* sin(x) f32 ~29 bits */
i = [0x1p-64, pi/4];
f = sin(x);
d = guessdegree(f, i, 0x1p-29);
d;
b = [|1, 3, 5, 7, 9, 11, 13|];
c = [|SG, SG, SG...|];
p = fpminimax(f, b, c, i,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float sin_c", "sin", b, c);
/*implementpoly(p, i, 0x1p-29, single, "__sin_k", "sin32.c");*/
plot((p(x)-f(x))/f(x), p(x)-f(x), i,
                       postscriptfile, "sin-f32-ep");

/* cos(x) f32 ~29 bits*/
i = [0x1p-64, pi/4];
f = cos(x);
d = guessdegree(f, i, 0x1p-29);
d;
b = [|0, 2, 4, 6, 8, 10, 12|];
c = [|SG, SG, SG, SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float cos_c", "cos", b, c);
/*implementpoly(p, i, 0x1p-29, single, "__cos_k", "cos32.c");*/
plot((p(x)-f(x))/f(x), p(x)-f(x), i,
                       postscriptfile, "cos-f32-ep");


/* asinh(x) f32 */
f = asinh(x);
i = [0, sqrt(2)/2];
d = guessdegree(f, i, 0x1p-28);
d;
b = [|1, 3, 5, 7, 9, 11, 13, 15|];
c = [|SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float asinh_i0_c", "asinh_i0", b, c);
plot((p(x)-f(x))/f(x), i, postscriptfile, "asinh_i0-f32");

il= sqrt(2)/2;
ir= 3.0;
im= dirtyfindzeros(asinh(x)-(0.5*(asinh(il)+asinh(ir))), [il, ir])[0];
im= round(im, 10, RN);
i = [il-im, ir-im];
i;
im;
x0= im;
f = asinh(x+x0);
d = guessdegree(f, i, 0x1p-28);
d;
b = [|0, ..., 15|];
c = [|49, SG...|];
p = fpminimax(f, b, c, i, relative, floating);
p;
write_approx_prec_shift(p, f, i, im);
write_coeffs(p, "constexpr\n    const float asinh_i1_c", "asinh_i1", b, c);
write_coeff(il, "", "constexpr\n    const float asinh_i1_left", single);
write_coeff(ir, "", "constexpr\n    const float asinh_i1_right", single);
write_coeff(x0, "", "constexpr\n    const float asinh_i1_x0", single);
plot((p(x)-f(x))/f(x), i, postscriptfile, "asinh_i1-f32");

/* acosh f32 */
i = [0x1p-30, 1.0];
f = acosh(1+x)/sqrt(2*x);
d = guessdegree(f, i, 0x1p-28);
d;
b = [|0, ..., 7|];
c = [|SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float acosh_i0_c", "acosh_i0", b, c);
plot((p(x)-f(x))/f(x), i,
                       postscriptfile, "acosh_i0-f32");

il= 2.0;
ir= 3.5;
im= dirtyfindzeros(acosh(x)-(0.5*(acosh(il)+acosh(ir))), [il, ir])[0];
im= round(im, 10, RN);
i = [il-im, ir-im];
i;
im;
x0= im;
f = acosh(x+x0);
d = guessdegree(f, i, 0x1p-28);
d;
b = [|0, ..., 10|];
c = [|49, SG...|];
p = fpminimax(f, b, c, i, relative, floating);
p;
write_approx_prec_shift(p, f, i, im);
write_coeffs(p, "constexpr\n    const float acosh_i1_c", "acosh_i1", b, c);
write_coeff(il, "", "constexpr\n    const float acosh_i1_left", single);
write_coeff(ir, "", "constexpr\n    const float acosh_i1_right", single);
write_coeff(x0, "", "constexpr\n    const float acosh_i1_x0", single);
plot((p(x)-f(x))/f(x), i, postscriptfile, "acosh_i1-f32");

/* atanh(x) piecewise polynomial approximations */
i = [0, 0.5];
f = atanh(x);
d = guessdegree(f, i, 0x1p-28);
d;
b = [|1, 3, 5, 7, 9, 11, 13|];
c = [|SG...|];
p = fpminimax(f, b, c, i, relative, floating);
p;
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const float atanh_i0_c", "atanh_i0", b, c);
plot((p(x)-f(x))/f(x), i, postscriptfile, "atanh_i0-f32");

il= 0.5;
ir= /* sqrt(2)/2*/ 0.75;
im= dirtyfindzeros(atanh(x)-(0.5*(atanh(il)+atanh(ir))), [il, ir])[0];
im= round(im, 10, RN);
i = [il-im, ir-im];
i;
im;
x0= im;
f = atanh(x+x0);
d = guessdegree(f, i, 0x1p-28);
d;
b = [|0, ..., 9|];
c = [|49, SG...|];
p = fpminimax(f, b, c, i, relative, floating);
p;
write_approx_prec_shift(p, f, i, im);
write_coeffs(p, "constexpr\n    const float atanh_i1_c", "atanh_i1", b, c);
write_coeff(il, "", "constexpr\n    const float atanh_i1_left", single);
write_coeff(x0, "", "constexpr\n    const float atanh_i1_x0", single);
plot((p(x)-f(x))/f(x), i, postscriptfile, "atanh_i1-f32");

/*
 * Local variables:
 * mode: c++
 * end:
 */
