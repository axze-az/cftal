#! /usr/bin/sollya
execute("common.sollya");
ext_lngamma = library("./libsollya_funcs.so");
ext_tgamma = library("./libsollya_funcs.so");
verbosity=1!;
prec=512!;
write("\n");

lngamma_12_table_f32 = proc()
{
    var xi, xe, d, f, i0, b, b12, rev_b, c, c12, rev_c, p, j, ci;
    d=1/32;
    xi=1.0;
    xe=2.0;
    b = [|0, ..., 5|];
    b12=[|1, ..., 5|];
    c = [|49, 49, 49, SG, SG, SG|];
    c12=    [|49, 49, SG, SG, SG|];
    i0= [-d/2, d/2];
    rev_c = revert(c);
    rev_b = revert(b);
    write("const float lngamma_12_table[]={\n");
    while (xi <= xe) do {
        f= ext_lngamma(_x_+xi);
        if (xi == 1.0) then {
            i0=[0, d/2];
            p=fpminimax(f, b12, c12, i0, relative, floating);
            p=p+0;
        } else if (xi==2.0) then {
            i0=[-d/2, 0];
            p=fpminimax(f, b12, c12, i0, relative, floating);
            p=p+0;
        } else {
            i0= [-d/2, d/2];
            p=fpminimax(f, b, c, i0, floating, floating);
        };
        write("    // ", xi, ": ");
        write_approx_prec(p, f, i0);
        j=0;
        for i in rev_b
        do {
            ci = coeff(p, i);
            match(rev_c[j]) with
            single: {
                write("    // x^ ", i , ": ");
                write_f_hex(ci);
                write(",\n");
                write("    ");
                write_f(ci);
                write(",\n");
            }
            49: {
                write("    // x^ ", i , ": ");
                write_df_h_hex(ci);
                write(",\n");
                write("    ");
                write_df_h(ci);
                write(",\n");
                write("    // x^ ", i , ": ");
                write_df_l_hex(ci);
                write(",\n");
                write("    ");
                write_df_l(ci);
                write(",\n");
            };

            j = j + 1;
        };
        xi = xi + d;
    };
    write("};\n");
};
lngamma_12_table_f32();

i0=[1.0+0x1p-256, 2.0-0x1p-256];
f= ext_lngamma(x)/((x-1)*(x-2));
b = [|0, ..., 15|];
c = [|49, 49, 49, 49, 49, 49, 49, SG... |];
d = guessdegree(f, i0, 0x1p-30);
d;
p = fpminimax(f, b, c, i0, relative, floating);
write_approx_prec(p, f, i0);
write_coeffs(p, "const float lngamma_i0_c", "lngamma", b, c);
plot((p(x)-f(x))/f(x), i0, postscriptfile, "tgamma_i0-f32");

/* for f64 usage in f32 functions: */
i0=[1.0+0x1p-256, 2.0-0x1p-256];
f= ext_lngamma(x)/((x-1)*(x-2));
b = [|0, ..., 15|];
c = [|D, D...|];
d = guessdegree(f, i0, 0x1p-30);
d;
p = fpminimax(f, b, c, i0, relative, floating);
write_approx_prec(p, f, i0);
write_coeffs(p, "const double lngamma_i0_c", "lngamma", b, c);
plot((p(x)-f(x))/f(x), i0, postscriptfile, "tgamma_i0-f32-f64");
quit;

/* implementpoly(p, [-log(2)/512, log(2)/512], 1e-30, DD, "exp_p", "exp.c"); */
/* printexpansion(p-1.0); */

/* erf(x) as polynomial between 0 and 0.75 */
f = erf(x);
i = [0, 0.75];
d = guessdegree(f, i, 0x1p-28);
d;
b = [|1, 3, 5, 7, 9, 11|];
c = [|49, SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "const float erf_i0_c", "erf_i0", b, c);
//write_coeff(2/sqrt(pi), "", "const float two_o_sqrt_pi", doubledouble);
plot((p(x)-f(x))/f(x), i, postscriptfile, "erf_i0-f32");

/* erf(x) piecewise polynomial approximations */
il= 0.75;
ir= 1.75;
im= (ir+il)/2;
i = [il-im, ir-im];
i;
im;
x0= im;
f = erf(x+x0);
d = guessdegree(f, i, 0x1p-28);
d;
b = [|0, ..., 10|];
c = [|49, SG...|];
p = fpminimax(f, b, c, i, relative, floating);
p;
write_approx_prec_shift(p, f, i, im);
write_coeffs(p, "const float erf_i1_c", "erf_i1", b, c);
write_coeff(il, "", "const float erf_i1_left", single);
write_coeff(x0, "", "const float erf_i1_x0", single);
plot((p(x)-f(x))/f(x), i, postscriptfile, "erf_i1-f32");

il= 1.75;
ir= 3.00;
im= (ir+il)/2;
i = [il-im, ir-im];
i;
im;
x0= im;
f = erf(x+x0);
d = guessdegree(f, i, 0x1p-28);
d;
b = [|0, ..., 10|];
c = [|49, SG...|];
p = fpminimax(f, b, c, i, relative, floating);
p;
write_approx_prec_shift(p, f, i, im);
write_coeffs(p, "const float erf_i2_c", "erf_i2", b, c);
write_coeff(il, "", "const float erf_i2_left", single);
write_coeff(x0, "", "const float erf_i2_x0", single);
plot((p(x)-f(x))/f(x), i, postscriptfile, "erf_i2-f32");

il= 3.00;
ir= 4.00;
im= (ir+il)/2;
i = [il-im, ir-im];
i;
im;
x0= im;
f = erf(x+x0);
d = guessdegree(f, i, 0x1p-28);
d;
b = [|0, ..., 10|];
c = [|49, SG...|];
p = fpminimax(f, b, c, i, relative, floating);
p;
write_approx_prec_shift(p, f, i, im);
write_coeffs(p, "const float erf_i3_c", "erf_i3", b, c);
write_coeff(il, "", "const float erf_i3_left", single);
write_coeff(x0, "", "const float erf_i3_x0", single);
plot((p(x)-f(x))/f(x), i, postscriptfile, "erf_i3-f32");

/* erfc(x) as polynomial between 0 and 0.75 */
f = erfc(x);
i = [0, 0.75];
d = guessdegree(f, i, 0x1p-28);
d;
b = [|0, 1, 3, 5, 7, 9, 11|];
c = [|SG...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "const float erfc_i0_c", "erfc_i0", b, c);
plot((p(x)-f(x))/f(x), i, postscriptfile, "erfc_i0-f32");


g= exp(x*x)*x*erfc(x);
/* erfc(x) piecewise polynomial approximations */
il= 0.75;
ir= 2.00;
im= dirtyfindzeros(g(x)-(0.5*(g(il)+g(ir))), [il, ir])[0];
im= round(im, 12, RN);
i = [il-im, ir-im];
i;
im;
x0= im;
f = g(x+x0);
d = guessdegree(f, i, 0x1p-28);
d;
b = [|0, ..., 11|];
c = [|49, SG...|];
p = fpminimax(f, b, c, i, relative, floating);
p;
write_approx_prec_shift(p, f, i, im);
write_coeffs(p, "const float erfc_i1_c", "erfc_i1", b, c);
write_coeff(il, "", "const float erfc_i1_left", single);
write_coeff(x0, "", "const float erfc_i1_x0", single);
plot((p(x)-f(x))/f(x), i, postscriptfile, "erfc_i1-f32");

g= exp(x*x)*x*erfc(x);
il= 2.0;
ir= 4.0;
im= dirtyfindzeros(g(x)-(0.5*(g(il)+g(ir))), [il, ir])[0];
im= round(im, 12, RN);
i = [il-im, ir-im];
i;
im;
x0= im;
f = g(x+x0);
d = guessdegree(f, i, 0x1p-28);
d;
b = [|0, ..., 11|];
c = [|49, SG...|];
p = fpminimax(f, b, c, i, relative, floating);
p;
write_approx_prec_shift(p, f, i, im);
write_coeffs(p, "const float erfc_i2_c", "erfc_i2", b, c);
write_coeff(il, "", "const float erfc_i2_left", single);
write_coeff(x0, "", "const float erfc_i2_x0", single);
plot((p(x)-f(x))/f(x), i, postscriptfile, "erfc_i2-f32");

i=[1/4.0, 1/10.0125];
deg=6;
f=exp(1/x*1/x)*1/x*erfc(1/x);
b=[|0, ..., deg|];
c= [|49, SG...|];
p=fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "const float erfc_i3_c", "erfc", b, c);
plot(log(abs(p(x)-f(x))), i, postscriptfile, "erfc_i3-f32");



/*
 * Local variables:
 * mode: c++
 * end:
 */
