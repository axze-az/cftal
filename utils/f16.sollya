#! /usr/bin/sollya
execute("common.sollya");

verbosity=1!;
prec=512!;
write("\n");

/* exp(x) f16 using tang's method */
N = 32;
f = exp(x);
f1 = expm1(x);
g=log(2)/2;
i=[-g/N, g/N];
exp_table(N, halfprecision);
b = [|1, 2|];
c = [|HP...|];
p = fpminimax(f1, b, c, i, relative, floating);
write_approx_prec(p, f1, i);
write_coeffs(p, "constexpr\n    const f16_t exp_c", "exp", b, c);

/* N/ln(2) */
write_coeff(N/log(2), "", "constexpr\n    const f16_t _32_ln2",
            halfprecision);
/* ln(2)/N Cody and Waite */
c1h=round(log(2)/N, 11-3-4, RN);
c1l=log(2)/N-c1h;
write_coeff(c1h, "", "constexpr\n    const f16_t _ln2_32_cw_h",
            halfprecision);
write_coeff(c1l, "", "constexpr\n    const f16_t _ln2_32_cw_l",
            halfprecision);
/* ln(2)/N */
c1h=halfprecision(log(2)/N);
c1l=log(2)/N-c1h;
write_coeff(c1h, "", "constexpr\n    const f16_t _ln2_32_h",
            halfprecision);
write_coeff(c1l, "", "constexpr\n    const f16_t _ln2_32_l",
            halfprecision);

/* log_10(x) = lg(10) */
/* N/lg(2) */
write_coeff(N/(log(2)/log(10)), "",
            "constexpr\n    const f16_t _32_lg2", halfprecision);
/* lg(2)/N Cody and Waite */
lg_2_N=log(2)/(log(10))/N;
c1h=round(lg_2_N, 11-3-4, RN);
c1l=lg_2_N-c1h;
write_coeff(c1h, "", "constexpr\n    const f16_t _lg2_32_cw_h",
            halfprecision);
write_coeff(c1l, "", "constexpr\n    const f16_t _lg2_32_cw_l",
            halfprecision);
/* lg(2)/N */
c1h=halfprecision(lg_2_N);
c1l=lg_2_N-c1h;
write_coeff(c1h, "", "constexpr\n    const f16_t _lg2_32_h",
            halfprecision);
write_coeff(c1l, "", "constexpr\n    const f16_t _lg2_32_l",
            halfprecision);

/* a simple polynomial for exp f16 */
f = exp(x);
i = [-log(2)/2, log(2)/2];
d = guessdegree(f, i, 0x1p-14);
d;
b = [|0, ..., 4|];
c = [|HP...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const f16_t exp_c", "exp", b, c);
plot((p(x)-f(x))/f(x), i, postscriptfile, "exp-f16");

/* a simple polynomial for higher precision exp f16 */
f = exp(x);
i = [-log(2)/2, log(2)/2];
d = guessdegree(f, i, 0x1p-14);
d;
b = [|0, ..., 5|];
c = [|HP...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const f16_t pow_exp_c", "pow_exp", b, c);
plot((p(x)-f(x))/f(x), i, postscriptfile, "pow-exp-f16");


/* polynomial for sinh between 0 and 1.0 */
f = sinh(x);
i = [0, 1.0];
d = guessdegree(f, i, 0x1p-13);
d;
b = [|1, 3, 5|];
c = [|HP...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const f16_t sinh_c", "sinh", b, c);
write_coeff(sup(i), "", "constexpr\n    const f16_t sinh_i0_right",
            halfprecision);

/* polynomial for sinh where (e^x-e^x)/2 fails */
f = sinh(x);
i = [0x1p-512, log(2)/4];
d = guessdegree(f, i, 0x1p-13);
d;
b = [|1|];
c = [|HP...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const f16_t sinh_c", "sinh", b, c);
write_coeff(sup(i), "", "constexpr\n    const f16_t sinh_i0_right",
            halfprecision);

/* polynomial for cosh between 0 and 1.0 */
f = cosh(x);
i = [0, 1.0];
d = guessdegree(f, i, 0x1p-13);
d;
b = [|0, 2, 4|];
c = [|HP...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const f16_t cosh_c", "cosh", b, c);
write_coeff(sup(i), "", "constexpr\n    const f16_t cosh_i0_right",
            halfprecision);

/* polynomial for tanh where (e^2x-1)(e^2x+1) fails */
f = tanh(x);
i = [0x1p-512, log(2)/32];
d = guessdegree(f, i, 0x1p-13);
d;
b = [| 1|];
c = [|HP...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const f16_t tanh_c", "tanh", b, c);
write_coeff(sup(i), "", "constexpr\n    const f16_t tanh_i0_right",
            halfprecision);

/* log1p for log implementation */
f= log1p(x);
ib=halfprecision(sqrt(2)/2);
write_coeff(ib, "", "constexpr\n    const f16_t sqrt2_2",
            halfprecision);
i= [ib-1, (ib*2)-1];
d = guessdegree(f, i, 0x1p-13);
d;
c = [|HP...|];
b = [|1, ..., 6|];
p = fpminimax(f, b, c, i, relative, floating);
// b = [|3, ..., 6|];
// p = fpminimax(f, b, c, i, relative, floating, x-0.5*x*x);
// b =[|1, 2|]@b;
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const f16_t log_c", "log", b, c);

/* sin(x) f32 */
i = [0x1p-58, pi/4];
f = sin(x)/x;
d = guessdegree(f, i, 0x1p-14);
d;
b = [|2,4|];
c = [|HP...|];
p = fpminimax(f, b, c, i, 1.0,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const f16_t sin_c", "sin", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x), i,
                       postscriptfile, "sin-f16");

/* cos(x) f32 */
i = [0x1p-58, pi/4];
f = cos(x)-1+(x^2)/2;
d = guessdegree(f, i, 0x1p-14);
d;
b = [|4, 6|];
c = [|HP...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const f16_t cos_c", "cos", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x), i,
                       postscriptfile, "cos-f32");

/* tan as polynomial in [0, pi/4] */
f = tan(x);
i = [0, pi/4];
d = guessdegree(f, i, 0x1p-14);
d;
b = [|1, 3, 5, 7|];
c = [|HP ...|];
p = fpminimax(f, b, c, i,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const f16_t tan_c", "tan", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x), i,
                       postscriptfile, "tan-f32");

pi_2=pi/2;
pi_2_h=halfprecision(pi_2);
pi_2= pi_2 - pi_2_h;
pi_2_m=halfprecision(pi_2);
pi_2 = pi_2 - pi_2_m;
pi_2_l=halfprecision(pi_2);
pi_2=pi/2;
write("\n");
write_coeff(-pi_2_h, "", "constexpr\n    const f16_t m_pi_2_h", halfprecision);
write_coeff(-pi_2_m, "", "constexpr\n    const f16_t m_pi_2_m", halfprecision);
write_coeff(-pi_2_l, "", "constexpr\n    const f16_t m_pi_2_l", halfprecision);


/* cbrt(x) f16, 5 bits precision  */
i = [2^-3, 1];
f = (x)^(1/3);
d = guessdegree(f, i, 0x1p-8);
d;
b= [|0,...,2|];
c= [|HP...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const f16_t cbrt_c", "cbrt", b, c);

/* rcbrt(x) f16, 5 bits precision */
i = [2^-3, 1];
f = (x)^-(1/3);
d = guessdegree(f, i, 0x1p-8);
d;
b= [|0,...,2|];
c= [|HP...|];
p = fpminimax(f, b, c, i, relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const f16_t rcbrt_c", "rcbrt", b, c);

/* root12 f16 */
f= x^(1/12);
i= [2^(-12), 2^(-9)];
d = guessdegree(f, i, 0x1p-8);
d;
b = [|0, ..., 2|];
c = [|HP...|];
p = fpminimax(f, b, c, i,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const f16_t root12_i0_c", "root12_i0", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x),
                       i,
                       postscriptfile, "root12-i0-f16");

/* root12 f16 */
f= x^(1/12);
i= [2^(-9), 2^(-6)];
d = guessdegree(f, i, 0x1p-8);
d;
b = [|0, ..., 2|];
c = [|HP...|];
p = fpminimax(f, b, c, i,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const f16_t root12_i1_c", "root12_i1", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x),
                       i,
                       postscriptfile, "root12-i1-f16");

/* root12 f32 */
f= x^(1/12);
i= [2^(-6), 2^(-3)];
d = guessdegree(f, i, 0x1p-8);
d;
b = [|0, ..., 2|];
c = [|HP...|];
p = fpminimax(f, b, c, i,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const f16_t root12_i2_c", "root12_i2", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x),
                       i,
                       postscriptfile, "root12-i2-f16");

/* root12 f32 */
f= x^(1/12);
i= [2^(-3), 2^(-0)];
d = guessdegree(f, i, 0x1p-8);
d;
b = [|0, ..., 2|];
c = [|HP...|];
p = fpminimax(f, b, c, i,
              relative, floating);
write_approx_prec(p, f, i);
write_coeffs(p, "constexpr\n    const f16_t root12_i3_c", "root12_i3", b, c);
plot((p(x)-f(x))/f(x), p(x)-f(x),
                       i,
                       postscriptfile, "root12-i3-f16");

/*
 * Local variables:
 * mode: c++
 * end:
 */
