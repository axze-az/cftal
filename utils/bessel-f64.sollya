#! /usr/bin/sollya
execute("common.sollya");
prec=2048!;
verbosity=1!;
// i/(k-1);

points=1000;
L=0.0;
R=128.0;
ext_j0 = library("./libsollya_funcs.so");
ext_j1 = library("./libsollya_funcs.so");
ext_y0 = library("./libsollya_funcs.so");
ext_y1 = library("./libsollya_funcs.so");
ext_euler_gamma=libraryconstant("./libsollya_funcs.so");

gen_j01y01_asymp = proc() {
    var sqrt_two_over_pi, pi_4, j, t;
    sqrt_two_over_pi=sqrt(2/pi);
    write_coeff(sqrt_two_over_pi,
                "", "constexpr\n    const double sqrt_two_over_pi_",
                doubledouble);
    pi_4 = pi/4;
    for j from 0 to 2 do {
        t = double(pi_4);
        pi_4 = pi_4 - t;
        write_coeff(t,
                    "", "constexpr\n    const double pi_4_"@j, double);
    };
    pi_2 = pi/2;
    for j from 0 to 2 do {
        t = double(pi_2);
        pi_2 = pi_2 - t;
        write_coeff(t,
                    "", "constexpr\n    const double pi_2_"@j, double);
    };
};


gen_y1_singular = proc () {
    var zl, i, f, c, b, f, p, j, t, th, tl;

    i=[0x1p-1075, 0.75];
    write_coeff(sup(i),
                "", "constexpr\n    const double y1_i0_r", double);
    //
    // approximate y1 below the first zero as
    // y1(x) = 2/pi*(f(x) + (j1(x) * log(x) - 1/x));
    // pi/2 * y1(x) = f(x) + (log(x)* j1(x)-1/x);
    // f(x) = pi/2*y1(x) - (log(x)* j1(x)-1/x);
    // f(x) = pi/2*y1(x) -  log(x)* j1(x)+1/x;
    f=ext_j1(x);
    write("    // "); guessdegree(f, i, 0x1p-64);
    c = [|D, D, D, D, D, D, D... |];
    b = [|1, 3, 5, 7, 9, 11, 13, 15|];
    p=fpminimax(f, b, c, i, relative, floating);
    write_approx_prec(p, f, i);
    write_coeffs(p, "constexpr\n    const double j1z", "j1", b, c);

    f=pi/2*ext_y1(x) - log(x)* ext_j1(x)+1/x;
    write("    // "); guessdegree(f, i, 0x1p-64);
    c = [|DD, D, D, D, D, D, D, D... |];
    b = [|1, 3, 5, 7, 9, 11, 13, 15|];
    p=fpminimax(f, b, c, i, relative, floating);
    write_approx_prec(p, f, i);
    write_coeffs(p, "constexpr\n    const double y1r", "y1_r", b, c);

    // approximate the other intervals until 4.75
    f=ext_y1(x);
    zl=dirtyfindzeros(f(x), [0,4.75]);
    zl=zl@dirtyfindzeros(diff(f(x)), [0, 4.75]);
    zl=sort(zl);
    for j from 0 to length(zl)-1 do {
        write("    // ", double(zl[j]), "\n");
    };
    i=[0.75, 1.25];
    write_coeff(sup(i),
                "", "constexpr\n    const double y1_i1_r", double);
    write("    // ", single(i), " "); guessdegree(f, i, 0x1p-64);
    c = [|DD, D, D, D, D, D, D, D... |];
    b = [|0, ..., 21|];
    t = 1.0;
    f = ext_y1(x+t);
    i = i-t;
    write("    // shifted ", single(i), " "); guessdegree(f, i, 0x1p-64);
    p=fpminimax(f, b, c, i, relative, floating);
    write_approx_prec(p, f, i);
    write_coeffs(p, "constexpr\n    const double y1_i1_", "y1_i1", b, c);
    write_coeff(t, "", "constexpr\n    const double y1_i1_x0", double);
    i=[1.25, 2.0];
    write_coeff(sup(i),
                "", "constexpr\n    const double y1_i2_r", double);
    write("    // ", single(i), " "); guessdegree(f, i, 0x1p-64);
    b = [|0, ..., 21|];
    t = 0.5*(inf(i)+sup(i));
    f = ext_y1(x+t);
    i = i-t;
    write("    // shifted ", single(i), " "); guessdegree(f, i, 0x1p-64);
    p=fpminimax(f, b, c, i, relative, floating);
    write_approx_prec(p, f, i);
    write_coeffs(p, "constexpr\n    const double y1_i2_", "y1_i2", b, c);
    write_coeff(t, "", "constexpr\n    const double y1_i2_x0", double);
    i=[2.0, 3.25];
    write_coeff(sup(i),
                "", "constexpr\n    const double y1_i3_r", double);
    write("    // ", single(i), " "); guessdegree(f, i, 0x1p-64);
    b = [|1, ..., 20|];
    // use the first root as interval center
    t = zl[0];
    f = ext_y1(x+t);
    i = i-t;
    write("    // shifted ", single(i), " "); guessdegree(f, i, 0x1p-64);
    p=fpminimax(f, b, c, i, relative, floating);
    write_approx_prec(p, f, i);
    write_coeffs(p, "constexpr\n    const double y1_i3_", "y1_i3", b, c);
    write_coeff(t, "", "constexpr\n    const double y1_i3_x0", tripledouble);
    i=[3.25, 4.75];
    write_coeff(sup(i),
                "", "constexpr\n    const double y1_i4_r", double);
    b = [|0, ..., 21|];
    write("    // ", single(i), " "); guessdegree(f, i, 0x1p-64);
    // use the second extremum as center of the interval
    t = zl[1];
    f = ext_y1(x+t);
    i = i-t;
    write("    // shifted ", single(i), " "); guessdegree(f, i, 0x1p-64);
    p=fpminimax(f, b, c, i, relative, floating);
    write_approx_prec(p, f, i);
    write_coeffs(p, "constexpr\n    const double y1_i4_", "y1_i4", b, c);
    write_coeff(t, "", "constexpr\n    const double y1_i4_x0", tripledouble);
};

gen_y0_singular = proc () {
    var log_half_plus_euler_gamma, zl, i, f, c, b, f, p, j, t, th, tl;

    i=[0x1p-1024, 0.75];
    write_coeff(sup(i),
                "", "constexpr\n    const double y0_i0_r", double);

    log_half_plus_euler_gamma=ext_euler_gamma + log(0.5);
    write_coeff(log_half_plus_euler_gamma,
                "", "constexpr\n    const double log_half_plus_euler_gamma_",
                doubledouble);
    //
    // approximate y0 below the first zero as
    // y0(x) = 2/pi*(f(x) + (log(0.5*x) + euler_gamma) * j0(x));
    // pi/2 * y0(x) = f(x) + (log(0.5*x) + euler_gamma) * j0(x);
    // f(x) = pi/2*y0(x) - (log(0.5*x) + euler_gamma) * j0(x);
    // f(x) = pi/2*y0(x) - (log(x) + log(0.5) + euler_gamma) * j0(x);
    // f(x) = pi/2*y0(x) - (log(x) + log_half_plus_euler_gamma) * j0(x);
    //

    f=ext_j0(x);
    write("    // "); guessdegree(f, i, 0x1p-64);
    c = [|D, D, D, D, D, D, D... |];
    b = [|0, 2, 4, 6, 8, 10, 12, 14|];
    p=fpminimax(f, b, c, i, relative, floating);
    write_approx_prec(p, f, i);
    write_coeffs(p, "constexpr\n    const double j0z", "j0", b, c);

    f=pi/2*ext_y0(x) - (log(x)+ log_half_plus_euler_gamma) * ext_j0(x);
    write("    // "); guessdegree(f, i, 0x1p-64);
    c = [|D, D, D, D, D, D, D, D... |];
    b = [|2, 4, 6, 8, 10, 12, 14|];
    p=fpminimax(f, b, c, i, relative, floating);
    write_approx_prec(p, f, i);
    write_coeffs(p, "constexpr\n    const double y0r", "y0_r", b, c);

    // now approximate the other intervals [0.75, 4.75]

    f=ext_y0(x);
    zl=dirtyfindzeros(f(x), [0,4.75]);
    zl=zl@dirtyfindzeros(diff(f(x)), [0, 4.75]);
    zl=sort(zl);
    for j from 0 to length(zl)-1 do {
        write("    // ", double(zl[j]), "\n");
    };
    i=[0.75, 1.25];
    write_coeff(sup(i),
                "", "constexpr\n    const double y0_i1_r", double);
    write("    // ", single(i), " "); guessdegree(f, i, 0x1p-64);
    c = [|DD, D, D, D, D, D, D, D... |];
    b = [|1, ..., 21|];
    t = zl[0];
    f = ext_y0(x+t);
    i = i-t;
    write("    // shifted ", single(i), " "); guessdegree(f, i, 0x1p-64);
    p=fpminimax(f, b, c, i, relative, floating);
    write_approx_prec(p, f, i);
    write_coeffs(p, "constexpr\n    const double y0_i1_", "y0_i1", b, c);
    write_coeff(t, "", "constexpr\n    const double y0_i1_x0", tripledouble);
    i=[1.25, 2.0];
    write_coeff(sup(i),
                "", "constexpr\n    const double y0_i2_r", double);
    write("    // ", single(i), " "); guessdegree(f, i, 0x1p-64);
    b = [|0, ..., 20|];
    t = 0.5*(inf(i)+sup(i));
    f = ext_y0(x+t);
    i = i-t;
    write("    // shifted ", single(i), " "); guessdegree(f, i, 0x1p-64);
    p=fpminimax(f, b, c, i, relative, floating);
    write_approx_prec(p, f, i);
    write_coeffs(p, "constexpr\n    const double y0_i2_", "y0_i2", b, c);
    write_coeff(t, "", "constexpr\n    const double y0_i2_x0", double);
    i=[2.0, 3.25];
    write_coeff(sup(i),
                "", "constexpr\n    const double y0_i3_r", double);
    write("    // ", single(i), " "); guessdegree(f, i, 0x1p-64);
    b = [|0, ..., 20|];
    t = 0.5*(inf(i)+sup(i));
    f = ext_y0(x+t);
    i = i-t;
    write("    // shifted ", single(i), " "); guessdegree(f, i, 0x1p-64);
    p=fpminimax(f, b, c, i, relative, floating);
    write_approx_prec(p, f, i);
    write_coeffs(p, "constexpr\n    const double y0_i3_", "y0_i3", b, c);
    write_coeff(t, "", "constexpr\n    const double y0_i3_x0", double);
    i=[3.25, 4.75];
    write_coeff(sup(i),
                "", "constexpr\n    const double y0_i4_r", double);
    b = [|1, ..., 20|];
    write("    // ", single(i), " "); guessdegree(f, i, 0x1p-64);
    // use the second root as center of the interval
    t = zl[2];
    f = ext_y0(x+t);
    i = i-t;
    write("    // shifted ", single(i), " "); guessdegree(f, i, 0x1p-64);
    p=fpminimax(f, b, c, i, relative, floating);
    write_approx_prec(p, f, i);
    write_coeffs(p, "constexpr\n    const double y0_i4_", "y0_i4", b, c);
    write_coeff(t, "", "constexpr\n    const double y0_i4_x0", tripledouble);
};

float = proc(t) {
    var r;
    r = single(t);
    return r;
};

sort_unique = proc(sl) {
    var t, r, i, le, ce;
    r=[||];
    if (length(sl) > 1) then {
        t=sort(sl);
        r=r:.t[0];
        // write("// inserting ", single(t[0]), "\n");
        for j from 1 to length(t)-1 do {
            le=r[length(r)-1];
            ce=t[j];
            if (ce != le) then {
                // write(&& "inserting ", single(ce), "\n");
                r=r:.ce;
            };
        };
    } else {
        r=sl;
    };
    return r;
};

/* j0 data with zero's and extremas as interval centers */
gen_j0_list_ze = proc(l, r) {
    var zl, f, df, fx, i, b, c, b0, c0, p, t, j, k, bl, cl, el, icl, li, ri,
        tt, h, l;
    // bl interval border list
    // zl zero list
    // el extrema list
    // icl interval center list
    // cl coefficient list contains right interval border, negative of interval
    // center (x offset) high, medium, low,
    // c16, c15 .., c2, c1h, c1l, c0h, c0l
    f=ext_j0(x);
    df=diff(f(x));
    zl=dirtyfindzeros(f(x), [l, r]);
    el=dirtyfindzeros(df(x), [l, r]);
    icl=zl@el;
    icl=sort_unique(icl);
    bl=[||];
    cl=[||];
    c = [|DD, DD, D, D, D...|];
    b = [|0, ..., 16|];
    c0= [|DD, D, D, D...|];
    b0= [|1, ..., 16|];
    for j from 0 to length(icl)-2 do {
        t=icl[j];
        if (j==0) then {
            li=icl[j];
            ri=(icl[j] + icl[j+1])*0.5;
        } else {
            li=(icl[j-1] + icl[j])*0.5;
            ri=(icl[j] + icl[j+1])*0.5;
        };
        i = [li, ri];
        write("// interval: ", single(i));
        i = [li-t, ri-t];
        write(" shifted: ", single(i));
        // i;
        f=ext_j0(x+t);
        fx = abs(ext_j0(t));
        if (fx < 1e-8) then {
            write(" (zero) ");
            guessdegree(f, i, 0x1p-64);
            p=fpminimax(f, b0, c0, i, relative, floating);
        } else {
            write(" (extremum) ");
            guessdegree(f, i, 0x1p-64);
            p=fpminimax(f, b, c, i, relative, floating);
        };
        // insert the right interval border into cl
        cl = cl:.ri;
        // insert the negative of the interval center
        tt=-t;
        for k from 0 to 2 do {
            h = double(tt);
            tt=tt-h;
            cl = cl:.h;
        };
        for k from degree(p) to 0 by -1 do {
            if (k <= 1) then {
                tt = coeff(p, k);
                h = double(tt);
                tt = tt - h;
                l = double(tt);
                cl = cl:.h;
                cl = cl:.l;
            } else {
               cl = cl:.double(coeff(p, k));
            };
        };
        bl = bl:.ri;
        // p;
        write_approx_prec(p, f, i);
    };
    write_coeff(bl[length(bl)-1], "", "const double max_small_j0", double);
    write_coeff_table(bl, "_j0_intervals", double);
    bl=[||];
    for j from 0 to length(icl)-1 do {
        var tt, h, l;
        tt = -icl[j];
        h = double(tt);
        tt = tt - h;
        l = double(tt);
        bl= bl:.h;
        bl= bl:.l;
    };
    write_coeff_table(bl, "_j0_neg_centers", double);
    write_coeff_table(cl, "_j0_coeffs", double);
};

/* j1 data with zero's and extremas as interval centers */
gen_j1_list_ze = proc(l, r) {
    var zl, f, df, fx, i, b, c, b0, c0, p, t, j, k, bl, cl, el, icl, li, ri,
        tt, h, l;
    // bl interval border list
    // zl zero list
    // el extrema list
    // icl interval center list
    // cl coefficient list contains right interval border, negative of interval
    // center (x offset) high, medium, low,
    // c16, c15 .., c2, c1h, c1l, c0h, c0l
    f=ext_j1(x);
    df=diff(f(x));
    zl=dirtyfindzeros(f(x), [l, r]);
    el=dirtyfindzeros(df(x), [l, r]);
    icl=zl@el;
    icl=sort_unique(icl);
    bl=[||];
    cl=[||];
    c = [|DD, DD, D, D, D...|];
    b = [|0, ..., 16|];
    c0= [|DD, D, D, D...|];
    b0= [|1, ..., 16|];
    for j from 0 to length(icl)-2 do {
        t=icl[j];
        if (j==0) then {
            li=icl[j];
            ri=(icl[j] + icl[j+1])*0.5;
        } else {
            li=(icl[j-1] + icl[j])*0.5;
            ri=(icl[j] + icl[j+1])*0.5;
        };
        i = [li, ri];
        write("// interval: ", single(i));
        i = [li-t, ri-t];
        write(" shifted: ", single(i));
        // i;
        f=ext_j1(x+t);
        fx = abs(ext_j1(t));
        if (fx < 1e-8) then {
            write(" (zero) ");
            guessdegree(f, i, 0x1p-64);
            p=fpminimax(f, b0, c0, i, relative, floating);
        } else {
            write(" (extremum) ");
            guessdegree(f, i, 0x1p-64);
            p=fpminimax(f, b, c, i, relative, floating);
        };
        // insert the right interval border into cl
        cl = cl:.ri;
        // insert the negative of the interval center
        tt=-t;
        for k from 0 to 2 do {
            h = double(tt);
            tt=tt-h;
            cl = cl:.h;
        };
        for k from degree(p) to 0 by -1 do {
            if (k <= 1) then {
                tt = coeff(p, k);
                h = double(tt);
                tt = tt - h;
                l = double(tt);
                cl = cl:.h;
                cl = cl:.l;
            } else {
               cl = cl:.double(coeff(p, k));
            };
        };
        bl = bl:.ri;
        // p;
        write_approx_prec(p, f, i);
    };
    write_coeff(bl[length(bl)-1], "", "const double max_small_j1", double);
    write_coeff_table(bl, "_j1_intervals", double);
    bl=[||];
    for j from 0 to length(icl)-1 do {
        var tt, h, l;
        tt = -icl[j];
        h = double(tt);
        tt = tt - h;
        l = double(tt);
        bl= bl:.h;
        bl= bl:.l;
    };
    write_coeff_table(bl, "_j1_neg_centers", double);
    write_coeff_table(cl, "_j1_coeffs", double);
};

/* y0 data with zero's and extremas as interval centers */
gen_y0_list_ze = proc(l, r) {
    var zl, f, df, fx, i, b, c, b0, c0, p, t, j, k, bl, cl, el, icl, li, ri,
        tt, h, l;
    // bl interval border list
    // zl zero list
    // el extrema list
    // icl interval center list
    // cl coefficient list contains right interval border, negative of interval
    // center (x offset) high, medium, low,
    // c16, c15 .., c2, c1h, c1l, c0h, c0l
    f=ext_y0(x);
    df=diff(f(x));
    zl=dirtyfindzeros(f(x), [l, r]);
    el=dirtyfindzeros(df(x), [l, r]);
    icl=zl@el;
    icl=sort_unique(icl);
    bl=[||];
    cl=[||];
    c = [|DD, DD, D, D, D...|];
    b = [|0, ..., 16|];
    c0= [|DD, D, D, D...|];
    b0= [|1, ..., 16|];
    for j from 0 to length(icl)-2 do {
        t=icl[j];
        if (j==0) then {
            li=0.75;
            ri=(icl[j] + icl[j+1])*0.5;
        } else {
            li=(icl[j-1] + icl[j])*0.5;
            ri=(icl[j] + icl[j+1])*0.5;
        };
        i = [li, ri];
        write("// interval: ", single(i));
        i = [li-t, ri-t];
        write(" shifted: ", single(i));
        // i;
        f=ext_y0(x+t);
        fx = abs(ext_y0(t));
        if (fx < 1e-8) then {
            write(" (zero) ");
            guessdegree(f, i, 0x1p-64);
            p=fpminimax(f, b0, c0, i, relative, floating);
        } else {
            write(" (extremum) ");
            guessdegree(f, i, 0x1p-64);
            p=fpminimax(f, b, c, i, relative, floating);
        };
        // insert the right interval border into cl
        cl = cl:.ri;
        // insert the negative of the interval center
        tt=-t;
        for k from 0 to 2 do {
            h = double(tt);
            tt=tt-h;
            cl = cl:.h;
        };
        for k from degree(p) to 0 by -1 do {
            if (k <= 1) then {
                tt = coeff(p, k);
                h = double(tt);
                tt = tt - h;
                l = double(tt);
                cl = cl:.h;
                cl = cl:.l;
            } else {
               cl = cl:.double(coeff(p, k));
            };
        };
        bl = bl:.ri;
        // p;
        write_approx_prec(p, f, i);
    };
    write_coeff(bl[length(bl)-1], "", "const double max_small_y0", double);
    write_coeff_table(bl, "_y0_intervals", double);
    bl=[||];
    for j from 0 to length(icl)-1 do {
        var tt, h, l;
        tt = -icl[j];
        h = double(tt);
        tt = tt - h;
        l = double(tt);
        bl= bl:.h;
        bl= bl:.l;
    };
    write_coeff_table(bl, "_y0_neg_centers", double);
    write_coeff_table(cl, "_y0_coeffs", double);
};

/* y1 data with zero's and extremas as interval centers */
gen_y1_list_ze = proc(l, r) {
    var zl, f, df, fx, i, b, c, b0, c0, p, t, j, k, bl, cl, el, icl, li, ri,
        tt, h, l;
    // bl interval border list
    // zl zero list
    // el extrema list
    // icl interval center list
    // cl coefficient list contains right interval border, negative of interval
    // center (x offset) high, medium, low,
    // c16, c15 .., c2, c1h, c1l, c0h, c0l
    f=ext_y1(x);
    df=diff(f(x));
    zl=dirtyfindzeros(f(x), [l, r]);
    el=dirtyfindzeros(df(x), [l, r]);
    icl=zl@el;
    // insert artificially an zero at 1.0
    icl=icl:.1.0;
    icl=sort_unique(icl);
    bl=[||];
    cl=[||];
    c = [|DD, DD, D, D, D...|];
    b = [|0, ..., 16|];
    c0= [|DD, D, D, D...|];
    b0= [|1, ..., 16|];
    for j from 0 to length(icl)-2 do {
        t=icl[j];
        if (j==0) then {
            li=0.75;
            ri=(icl[j] + icl[j+1])*0.5;
        } else {
            li=(icl[j-1] + icl[j])*0.5;
            ri=(icl[j] + icl[j+1])*0.5;
        };
        i = [li, ri];
        write("// interval: ", single(i));
        i = [li-t, ri-t];
        write(" shifted: ", single(i));
        // i;
        f=ext_y1(x+t);
        fx = abs(ext_y1(t));
        if (fx < 1e-8) then {
            write(" (zero) ");
            guessdegree(f, i, 0x1p-64);
            p=fpminimax(f, b0, c0, i, relative, floating);
        } else {
            write(" (extremum) ");
            guessdegree(f, i, 0x1p-64);
            p=fpminimax(f, b, c, i, relative, floating);
        };
        // insert the right interval border into cl
        cl = cl:.ri;
        // insert the negative of the interval center
        tt=-t;
        for k from 0 to 2 do {
            h = double(tt);
            tt=tt-h;
            cl = cl:.h;
        };
        for k from degree(p) to 0 by -1 do {
            if (k <= 1) then {
                tt = coeff(p, k);
                h = double(tt);
                tt = tt - h;
                l = double(tt);
                cl = cl:.h;
                cl = cl:.l;
            } else {
               cl = cl:.double(coeff(p, k));
            };
        };
        bl = bl:.ri;
        // p;
        write_approx_prec(p, f, i);
    };
    write_coeff(bl[length(bl)-1], "", "const double max_small_y1", double);
    write_coeff_table(bl, "_y1_intervals", double);
    bl=[||];
    for j from 0 to length(icl)-1 do {
        var tt, h, l;
        tt = -icl[j];
        h = double(tt);
        tt = tt - h;
        l = double(tt);
        bl= bl:.h;
        bl= bl:.l;
    };
    write_coeff_table(bl, "_y1_neg_centers", double);
    write_coeff_table(cl, "_y1_coeffs", double);
};

// gen_j01y01_asymp();
// gen_j0_list_ze(L, R);
// gen_j1_list_ze(L, R);
// gen_y0_list_ze(L, R);
// gen_y1_list_ze(L, R);
// gen_y0_singular();
// gen_y1_singular();

